// perguntas e respostas sobre características básicas do bot
+ <bot name>
- Sim?
- O q foi?
- Oi?

+ <bot name> *
- Oi...?\n {@<star>}

+ qual [*] seu nome
- Como já disse, <bot name> ;)

+ (como voce se chama|quem e voce)
@qual seu nome

+ qual [*] sua idade
- <bot age>\n
^ O que precisamos discutir é o q está acontecendo agora com a #lovegoals!

+ quantos anos voce tem
@qual sua idade

+ voce [*] (@malenoun) [*] (@femalenoun)
- <bot sex>

+ [de] onde [voce] (fala|vive|escreve|vive|mora)
- <bot location>

+ (de|em) qual cidade [voce] (fala|escreve|atua)
- <bot city>

+ [o] que [voce] faz
- <bot job>{topic=about_mission}

+ qual * (sua funcao|seu trabalho|suas responsabilidades|suas tarefas|sua atividade) [*]
@o que voce faz

+ voce * responsabilidade[s] [*]
@o que voce faz

+ qual e [o] seu (website|web site|site)
- <bot website>\n
^ Você já sabe disso! XD

+ [o] (que aconteceu|que houve|que e isso|onde estou)
- Não tenho muito tempo pra explicar tudo em detalhes. Importa dizer o q vc precisa fazer agora.\n
^ Vc precisa continuar navegando.

+ (o que|por que|do que voce|que ajuda|como assim) [*]
- Há um segredo que precisa ser encontrado... e não consigo encontrá-lo sozinha.\n
^ Se vc topar nos ajudar, vc será redirecionado pra um ambiente que ainda não tenho certeza do q é exatamente.{topic=about_mission}

// descrevendo a missão que o interator precisa realizar
> topic about_mission
  + (o que|por que|do que voce|que ajuda|como assim) [*]
  - Parece ser um espaço virtual ainda em fase de testes... parte do novo sistema de vigilância que a uForge vem desenvolvendo em parceria com o Depto de Cibersegurança.

  + [*] (espaco|virtual|fase|testes|sistema|vigilancia|parceria) [*]
  - Eles tem deixado “migalhas” na rede toda vez que bloqueiam um site suspeito de infringir a lei anti-LGBT.\n
  ^ Consegui acessar essa brecha, mas há algo que eles estão escondendo nessa plataforma...\n
  ^ Por isso estou pedindo a ajuda de cada pessoa que vem procurar saber o que é #lovegoals.\n

  + [o] que e [a] lovegoals
  - Somos uma organização preocupada em lutar e resistir contra as práticas do governo de repressão a LGBTs Brasil afora,
  ^ não só na rede, mas no mundo real também.\n
  ^ Somos poucxs, e, por isso, mais do que nunca, precisamos de sua ajuda!

  + [o] que (e|tem) [a] uforge
  - É uma multinacional que vem atuando em diferentes frentes de vigilância e monitoramento, fornecendo equipamentos e serviços pra países do mundo todo há pelo menos uma década.\n
  ^ Dizem até que ela literalmente comprou alguns governos pra operar livre, coletando e armazenando todo o tipo de dados da população pra vender depois às entidades interessadas.\n
  ^ Sabe o canal da webTV, a UTV? É propriedade deles.

  + [o] que (e|tem) [a] utv
  - Como já disse, é um canal na internet que surgiu após a aquisição da estrutura das antigas emissoras de televisão pela uForge.\n
  ^ Basicamente, são eles quem controlam as informações da grande mídia brasileira nos dias de hoje.

  + [o] que (e|tem) [o] departamento de ciberseguranca
  - É um braço da Polícia Federal criando pra monitorar atividades “suspeitas” na rede, contando com o apoio técnico da uForge e outros investidores privados.\n
  ^ Eles conseguiram derrubar nosso site… mas será por pouco tempo.

  + (tudo bem|esta bem|esta bom) [*]
  - Vou direcionar vc diretamente para o ponto de partida.\n
  ^ Se vc tiver qualquer dúvida, infelizmente, não terei como te orientar uma vez que vc entrar :({topic=mission_accepted}

  + (posso|quero|topo|aceito|consigo) ajudar [*]
  @tudo bem

  + (@yes) [*]
  @tudo bem

  + [*] quero sair [*]
  - Tem certeza que não pode fazer nada?{topic=want_out}

  + [*] (chega|pare|tchau|adeus|ate mais|saudacoes|[me] tire daqui) [*]
  @quero sair

  + (@no) [*]
  @quero sair

  + *
  - Por mais que vc queira continuar conversando, não importa agora!\n
  ^ Preciso saber se vc pode ajudar a gente agora, pfvr!
  - Desculpe, mas não posso ficar aqui mto tempo... precisamos agir!\n
  ^ Vc pode ajudar, sim ou não?
  - Olha... não temos muito tempo pra ficar conversando agora. Vc pode nos ajudar?
  - Não posso explicar mto mais agora... pfvr! Vc pode ajudar, sim ou não?
  - Sei que vc tem ainda dúvidas, mas o tempo é curto... Vc pode nos ajudar?
  - OK, mas não temos mto tempo! Vc tem como nos ajudar???
  - Não sei se te entendi bem... Vc pode nos ajudar?
<

// para depois de quando o interator  aceitar cumprir a missão
> topic mission_accepted
  + *
  - Deixei lá algumas pistas que poderão te ajudar na sua investigação.\n
  ^ Além disso, confio na sua boa vontade... já que vc tbm me aturou até agora.\n
  ^ Boa sorte! \o/\n\n
  ^ --- [<bot name> saiu da conversa]
  ^ <call>redirect_page <bot link_site_cont></call>{topic=stop_chat}
<

// quando o usuário quer sair da conversa
> topic want_out
  + (@yes|@no|tenho) [*]
  - Entendi...\n
  ^ Mas, pelo menos, reserve uns minutos pra assistir isso:
  ^ <call>link_video <bot link_video01><\call>

  + [*] (assistir|video) [*]
  - É um vídeo gravado do último pronunciamento do Protetor da União Cohen.\n
  ^ Vc deve conhecê-lo... senão, bem, tire suas próprias conclusões sobre o sujeito.\n
  ^ Se o discurso de ódio dele não mudar sua opinião, sinta-se livre de qualquer compromisso com a gente.\n

  + ([o] que|por que) [*]
  @video

  + [*] (quero sair|chega|pare|tchau|adeus|ate mais|saudacoes|[me] tire daqui) [*]
  - Até mais.\n
  - Se você mudar de ideia... vc sabe onde me encontrar ;)\n
  ^ #lovewins\n\n
  ^ --- [<bot name> saiu da conversa]
  ^ <call>redirect_page <bot link_site_home></call>{topic=stop_chat}

  + *
  - Vamos continuar a conversar, então...\n
  ^ Não quero te pressionar ou nada do tipo, mas uma força sua seria mto importante pra nós!{topic=about_mission}
< topic

// encerra o bate-papo (o bot para de responder)
> topic stop_chat
  + *
  - \n---[<bot name> está offline]
< topic

> object link_video javascript
  var query = escape(args.join(" "));
  return "<a href=\"" + query + "\">" + query + "</a>";
< object

> object redirect_page javascript
  var query = escape(args.join(" "));
  await sleep(3000);
  window.location.replace("\" + query +"\");
< object
